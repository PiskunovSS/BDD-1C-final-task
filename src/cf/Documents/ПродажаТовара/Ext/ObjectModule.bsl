
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Очистить();
	
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Док.Номенклатура КАК Номенклатура,
	|	СУММА(Док.Количество) КАК Количество
	|ПОМЕСТИТЬ ДокТЧ
	|ИЗ
	|	Документ.ПродажаТовара.Товары КАК Док
	|ГДЕ
	|	Док.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Док.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Номенклатура.Представление КАК НоменклатураПредставление,
	|	Остатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&ТочкаИтогов,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ДокТЧ.Номенклатура
	|				ИЗ
	|					ДокТЧ КАК ДокТЧ)) КАК Остатки
	|ГДЕ
	|	Остатки.КоличествоОстаток < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокТЧ.Номенклатура
	|ИЗ
	|	ДокТЧ КАК ДокТЧ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.УстановитьПараметр("ТочкаИтогов", Новый Граница(МоментВремени(), ВидГраницы.Включая)); 
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	РезультатЗапроса = ПакетРезультатов[1];
	
	
	Если НЕ РезультатЗапроса.Пустой() Тогда

		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Мало товара " + Выборка.НоменклатураПредставление + " нужно еще " + (-Выборка.КоличествоОстаток);
			Сообщение.Сообщить(); 
		КонецЦикла;  
	КонецЕсли; 
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры
